description = "chainbase â€“ a decentralized database for blockchain."

// Dependency versions
// ---------------------------------------

def junitVersion = "4.12"
def mockitoVersion = "2.1.0"
def testNgVersion = "6.11"
def jacocoVersion = "0.8.0"
def leveldbVersion = "1.8"
def jansiVersion = "1.16"
// --------------------------------------

static def isWindows() {
    return org.gradle.internal.os.OperatingSystem.current().isWindows()
}

if (isWindows()) {
    ext {
        leveldbGroup = "org.ethereum"
        leveldbName = "leveldbjni-all"
        leveldbVersion = "1.18.3"
    }
} else {
    ext {
        leveldbGroup = "org.fusesource.leveldbjni"
        leveldbName = "leveldbjni-all"
        leveldbVersion = "1.23"
    }
}

dependencies {
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"

    testImplementation "org.testng:testng:$testNgVersion"

    implementation group: leveldbGroup, name: leveldbName, version: leveldbVersion
    implementation "org.fusesource.jansi:jansi:$jansiVersion"
    // implementation group: 'org.rocksdb', name: 'rocksdbjni', version: '5.15.10'
    implementation files('../libs/rocksdbjni-6.20.3-osx.jar')
    implementation group: 'com.typesafe', name: 'config', version: '1.3.2'
    // implementation 'com.github.tronprotocol:zksnark-java-sdk:master-SNAPSHOT'
    implementation files('../libs/zksnark-java-sdk.jar')
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.5'
    implementation project(":protocol")
    implementation project(":common")
    implementation project(":crypto")
    implementation 'org.reflections:reflections:0.9.11'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.19.2'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69'
    implementation group: 'io.grpc', name: 'grpc-netty', version: '1.14.0'
}


test {

    testLogging {
        // showing skipped occasionally should prevent CI timeout due to lack of standard output
        events = ["skipped", "failed"] // "started", "passed"
        // showStandardStreams = true
        exceptionFormat = "full"

        debug.events = ["skipped", "failed"]
        debug.exceptionFormat = "full"

        info.events = ["failed", "skipped"]
        info.exceptionFormat = "full"

        warn.events = ["failed", "skipped"]
        warn.exceptionFormat = "full"
    }

    maxHeapSize = "1200m"

    if (System.getenv("CI") == null) {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
}

task testng(type: Test) {
    useTestNG()
    testLogging {
        events = ["skipped", "failed"]
        exceptionFormat = "full"

        debug.events = ["skipped", "failed"]
        debug.exceptionFormat = "full"

        info.events = ["failed", "skipped"]
        info.exceptionFormat = "full"

        warn.events = ["failed", "skipped"]
        warn.exceptionFormat = "full"
    }
}

check.dependsOn testng

jacoco {
    toolVersion = jacocoVersion // See http://www.eclemma.org/jacoco/.
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    executionData.from = '../framework/build/jacoco/jacocoTest.exec'
    afterEvaluate {
        classDirectories.from = classDirectories.files.collect {
            fileTree(dir: it,)
        }
    }
    dependsOn compileJava, processResources
}

build.dependsOn jacocoTestReport
