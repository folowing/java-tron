description = "actuator â€“ a series of transactions for blockchain."

// Dependency versions
// ---------------------------------------

def junitVersion = "4.12"
def mockitoVersion = "2.1.0"
def testNgVersion = "6.11"
def slf4jVersion = "1.7.25"
// --------------------------------------

dependencies {
    implementation project(":common")
    implementation project(":chainbase")
    implementation project(":protocol")
    implementation project(":crypto")
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"

    testImplementation "org.testng:testng:$testNgVersion"

    implementation "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69'
    // implementation 'com.github.tronprotocol:zksnark-java-sdk:master-SNAPSHOT'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.11'
    implementation 'org.reflections:reflections:0.9.11'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.19.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.5'
    implementation files('../libs/rocksdbjni-6.20.3-osx.jar')
    implementation group: 'org.fusesource.leveldbjni', name: 'leveldbjni-all', version: '1.23'
    implementation group: 'io.grpc', name: 'grpc-netty', version: '1.14.0'
}

test {

    testLogging {
        // showing skipped occasionally should prevent CI timeout due to lack of standard output
        events = ["skipped", "failed"] // "started", "passed"
        // showStandardStreams = true
        exceptionFormat = "full"

        debug.events = ["skipped", "failed"]
        debug.exceptionFormat = "full"

        info.events = ["failed", "skipped"]
        info.exceptionFormat = "full"

        warn.events = ["failed", "skipped"]
        warn.exceptionFormat = "full"
    }

    maxHeapSize = "1200m"

    if (System.getenv("CI") == null) {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
}

task testng(type: Test) {
    useTestNG()
    testLogging {
        events = ["skipped", "failed"]
        exceptionFormat = "full"

        debug.events = ["skipped", "failed"]
        debug.exceptionFormat = "full"

        info.events = ["failed", "skipped"]
        info.exceptionFormat = "full"

        warn.events = ["failed", "skipped"]
        warn.exceptionFormat = "full"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    executionData.from = '../framework/build/jacoco/jacocoTest.exec'
    afterEvaluate {
        classDirectories.from = classDirectories.files.collect {
            fileTree(dir: it,)
        }
    }
}

check.dependsOn testng
